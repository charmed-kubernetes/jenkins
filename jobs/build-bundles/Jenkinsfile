@Library('juju-pipeline@master') _

// List of bundles to release to the charmstore
def bundle_mapping = [
    'cdk-flannel': [
        fragments: 'k8s/cdk cni/flannel cri/containerd',
        charmstore: 'cs:~containers/bundle/canonical-kubernetes',
    ],
    'charmed-flannel': [
        fragments: 'k8s/cdk cni/flannel cri/containerd',
        charmstore: 'cs:~containers/bundle/charmed-kubernetes',
    ],
    'core-flannel': [
        fragments: 'k8s/core cni/flannel cri/containerd',
        charmstore: 'cs:~containers/bundle/kubernetes-core'
    ],
    'cdk-calico': [
        fragments: 'k8s/cdk cni/calico cri/containerd',
        charmstore: 'cs:~containers/bundle/kubernetes-calico'
    ],
    'cdk-canal': [
        fragments: 'k8s/cdk cni/canal cri/docker',
        charmstore: 'cs:~containers/bundle/canonical-kubernetes-canal'
    ],
    'kubernetes-tigera-secure-ee': [
        fragments: 'k8s/cdk cni/tigera-secure-ee cri/containerd',
        charmstore: 'cs:~containers/bundle/kubernetes-tigera-secure-ee'
    ]
]

def releaseBundle(bundle, charmstore) {
    // Releases bundle to defined charmstore namespace
    def revision = sh script:String.format("charm push %s %s  | tail -n +1 | head -1 | awk '{print \$2}'", bundle, charmstore), returnStdout: true
    sh script:String.format("charm release --channel %s %s", params.channel, revision.trim()), returnStatus: true
}

pipeline {
    agent {
        label 'runner-amd64'
    }
    /* XXX: Global $PATH setting doesn't translate properly in pipelines
     https://stackoverflow.com/questions/43987005/jenkins-does-not-recognize-command-sh
     */
    environment {
        PATH = "${utils.cipaths}"
        REPO_DIR = "${env.WORKSPACE}/bundle-kubernetes"
        BUNDLES_DIR = "${env.WORKSPACE}/tmp-bundles"
    }
    options {
        ansiColor('xterm')
        timestamps()
    }
    stages {
        stage('Setup') {
            steps {
                dir(env.REPO_DIR) {
                    git poll: true, url: params.bundle_repo
                }
            }
        }
        stage('Build Bundles') {
            steps {
                script {
                    def jobs = [:]
                    bundle_mapping.each { bundle, options ->
                        jobs[bundle] = {
                            stage(String.format("Build: %s", bundle)) {
                                sh script:String.format("%s/bundle -o %s/%s -c %s %s",
                                                        env.REPO_DIR,
                                                        env.BUNDLES_DIR,
                                                        bundle,
                                                        params.channel,
                                                        options.fragments), returnStatus: true
                            }
                        }
                    }
                    parallel jobs
                }
            }
        }
        stage('Release Bundles') {
            steps {
                script {
                    bundle_mapping.each { bundle, options ->
                        stage(String.format("Release: %s", bundle)) {
                            releaseBundle(String.format("%s/%s", env.BUNDLES_DIR, bundle), options.charmstore)
                        }
                    }
                }
            }
        }
    }
}
